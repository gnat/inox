# POST /sessions creates a new session and returns a session cookie if the received name corresponds to a user.
# Obviously never do this to log-in a user in a real app.
manifest {
    # Scheme of the request body.
    parameters: {
        username: %str
    }

    databases: /main.ix

    permissions: {
        read: ldb://main
        write: ldb://main
    }
}

import /schema.ix


# Retrieve the user.
username = mod-args.username
user = find_first!(%{name: $username}, dbs.main.users)

if (user?) {
    # Create a new session and send the id cookie.

    var new_session session = {
        id: "" # a random value will be set by the server.
        user-url: url_of(user)
    }

    return http.Result{
        status: http.status.SEE_OTHER
        headers: {
            Location: "/todos"
        },
        session: new_session
    }
}

# The user was not found.
return http.Result{
    status: http.status.BAD_REQUEST
    body: "Invalid credentials."
}