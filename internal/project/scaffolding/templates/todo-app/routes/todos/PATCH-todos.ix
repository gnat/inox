# PATCH /todos.ix updates the todo list. This endpoint supports creating, updating and deleting items.
# /todos is a single resource.
manifest {
    # Schema of the request body.
    parameters: {
        insertion: %optional({
            title: string
        })
        updates: %[]{
            key: string, 
            done: bool
        }
        deletions: %[]{
            key: string
        }
    }

    databases: /main.ix

    permissions: {
        read: ldb://main
        write: ldb://main
    }
}

import /schema.ix

# Check that the user is logged-in.
session = ctx_data(/session)

if (session == nil){
    return http.Result{
        status: http.status.FORBIDDEN
    }
} 

# Load the user.
assert (session match session) 
user = get!(session.user-url)

# Handle insertion.
if mod-args.insertion? {
    # Add the item.
    user::todo-items.append({
        title: mod-args.insertion.title
        done: false
        key: tostring(rand(%int))
    })
}

# Handle updates.
for item_update in mod-args.updates {
    item = find_first!(%{key: $item_update.key}, user.todo-items)
    if item? {
        item.done = item_update.done
    }
}

# Handle deletions.
for deletion in mod-args.deletions {
    key = deletion.key
    user::todo-items.remove_all(%{key: $key})
}

return http.Result{
    headers: {
        HX-Trigger: "listUpdate" # Rerender the todo list.
    }
}