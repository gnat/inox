# PATCH /todos.ix updates the todo items referenced by key.
manifest {
    # Schema of the request body.
    parameters: {
        updates: %[]{key: string, done: bool}
    }

    databases: /main.ix

    permissions: {
        read: ldb://main
        write: ldb://main
    }
}

import /schema.ix

# Check that the user is logged-in.
session = ctx_data(/session)

if (session == nil){
    return http.Result{
        status: http.status.FORBIDDEN
    }
} 

# Load the user.
assert (session match session) 
user = get!(session.user-url)

# Update the items.
for item_update in mod-args.updates {
    item = find_first!(%{key: $item_update.key}, user.todo-items)
    if item? {
        item.done = item_update.done
    }
}

return http.Result{
    headers: {
        HX-Trigger: "updatedTodo" # Rerender the todo list.
    }
}