# GET /todos renders the todo list.
manifest {
    databases: /main.ix

    permissions: {
        read: ldb://main
    }
}

import /schema.ix

# Check that the user is logged-in.
session = ctx_data(/session)

if (session == nil){
    return http.Result{
        status: http.status.FORBIDDEN
    }
} 

# Load the user.
assert (session match session) 
user = get!(session.user-url)


# Render the todo items and the item creation form.
return html<div>
    <form hx-patch-json="/todos">
        <input name="insertion.title" type="text" placeholder="new item" minlength="1" required/>
        <button type="submit">Add</button>
    </form>

    <ul>
        {
            map_iterable(user.todo-items, fn(item todo-item) => html<li> {Todo(item)} </li>)
        }
    </ul>

    <style>
        me {
            display: flex;
            flex-direction: column;
            gap: 10px;
        }

        me > form input {
            border: 1px solid grey;
            border-radius: 3px;
            padding: 7px;
        }

        me > ul {
            display: flex;
            flex-direction: column;
            gap: 5px;
        }

        me .todo-item {
            display: grid;
            grid-template-columns: 1fr 4fr 1fr;
            align-items: center;

            border: 1px solid grey;
            border-radius: 5px;
            padding: 7px;
        }

        me .todo-item button {
            background: none;
        }
    </style>
</div>

fn Todo(item {title: str, done: bool, key: string}){
    toggle_item_payload = asjson({
        updates: [{ key: item.key, done: !item.done}]
    })

    delete_item_payload = asjson({
        deletions: [{key: item.key}]
    })

    return html<div class="todo-item">
        <form hx-patch-json="/todos" jsonform-payload=toggle_item_payload>
            <button>
                {(if item.done "‚úÖ" else "‚¨ú")}
            </button>
        </form>
        
        <span class="title">
            {item.title}
        </span>

        <form hx-patch-json="/todos" jsonform-payload=delete_item_payload>
            <button>üóëÔ∏è</button>
        </form>
    </div>
}
