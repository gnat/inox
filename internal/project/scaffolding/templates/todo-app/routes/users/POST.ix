# /routes/users/POST.ix handles POST requests to /users.
# Each request is handled in a read-write transaction.

manifest {
    # Give access to the databases defined by /main.ix.
    databases: /main.ix

    # Schema of the request body (JSON).
    parameters: {
        username: %string
    }

    permissions: {
        read: ldb://main
        write: ldb://main
    }
}

import /schema.ix

username = mod-args.username
same_name_user = find_first!(%{name: $username}, dbs.main.users)

if same_name_user? {
    return http.Result{
        status: http.status.BAD_REQUEST
        body: "Name is already taken."
    }
}

# Create a user.
var user user = {
    name: username
    todo-items: []
}

dbs.main.users.add(user)

return http.Result{
    headers: {
        # (HTMX) Redirect to the login page.
        HX-Redirect: "/#login"
    }
}
