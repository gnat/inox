# this script generates docs/tutorials

const (
    DATA_FILE = ./internal/learn/tutorials.yaml
    OUTPUT_DIR = ./docs/tutorials/
)

manifest {
    permissions: {
        read: IWD_PREFIX
        write: IWD_PREFIX
        delete: IWD_PREFIX
    }
}


import ./base.ix

# Retrieve tutorials.

content = read!(DATA_FILE)
assert (content match [[]tutorial-series])

tutorial_series = content[0]

# Generate tutorials/README.md.
var readme str = "# Tutorial Series \n\n"

for series_index, series in tutorial_series {
    order = get_order_string(series_index)
    readme = concat readme `- [${series.name}](./${order}-${series.id}/)\n`
}

readme = concat readme `
It is recommended to follow through these tutorial series directly in VSCode by creating a file with a \`.tut.ix\` extension.
You will get syntax colorization. Make sure to create the file **inside a project**.

[Install Inox & VSCode Extension](https://github.com/inoxlang/inox#installation)

![tutorial-demo](https://github.com/inoxlang/inox-vscode/raw/master/assets/docs/tutorial-demo.gif)

`

# Delete tutorials directory.

delete!(OUTPUT_DIR)

# Add README

create!(OUTPUT_DIR.join(./README.md), readme)

# Generate a directory for each series.
for series_index, series in tutorial_series {
    order = get_order_string(series_index)
    series_dir = OUTPUT_DIR.join(./{order}-{series.id}/)
    fs.mkdir!(series_dir)

    var series_readme str = `# ${series.name}\n`

    # Generate a file for each tutorial in the series and a README for the series.
    for tut_index, tutorial in series.tutorials {
        tutorial_order = get_order_string(tut_index)
        tutorial_file_name = `${tutorial_order}-${tutorial.id}.md`
        tutorial_file = series_dir.join(./{tutorial_file_name})
        series_readme = concat series_readme `- [${tutorial.name}](./${tutorial_file_name})\n`

        program = strmanip.trim_space(tutorial.program)

        create!(tutorial_file, (concat "# " tutorial.name " \n\n```\n"  program "\n```"))
    }

    # Write the README for the series.
    create!(series_dir.join(./README.md), series_readme)
}
